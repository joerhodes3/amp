.title modern_tube_amp
*
* old opamp -- remove and include TI in compatibility mode
.include "/home/jrhodes/modern_tube_amp/spice/opamp_old.lib"
*
*.include "/home/jrhodes/modern_tube_amp/spice/opa1641.lib"
*.include "/home/jrhodes/modern_tube_amp/spice/OPA2156.lib"
*
* input -- RCA and/or XLR+ (pin 2)
V1 IN 0 DC SIN(0 1.2 1k)
* in truth /Vgnd and GND will likely differ by 12V or so -- that is why they are isolated
Rsim_vgnd1 /Vgnd 0 1
*
* ?? want V2 as 180 out of phase with V1
* XLR- (pin 3)
*V2 IN- 0 DC -V1 ; also tried -V(IN)

* board power
V3 /pwr GND DC 5

* op amp power
V4 /VEE /Vgnd DC -12
V5 /VCC /Vgnd DC 12

* tube heaters -- make soft start, RC delay ramps up slowly?
V6 /H1 /HGND_12ax7 DC 6 
V7 /H2 /HGND_12ax7 DC 6 
V8 /H3 /HGND_KT88 DC 6 
V9 /H4 /HGND_KT88 DC 6 
* for sim let /HGND be GND -- in real life, isolated
RHGND1 /HGND_12ax7 GND 1
RHGND2 /HGND_KT88 GND 1

* tube power
V10 /B+ GND DC 460 

*********************** ss pre amp
* op pwr
C23 /Vgnd /VCC 300u
C24 /VEE /Vgnd 300u
C25 /Vgnd /VCC 300u
C26 /VEE /Vgnd 300u
C27 /Vgnd /VCC 300u
C28 /VEE /Vgnd 300u

* preamp power
C19 /Vgnd /VCC 100n
C20 /Vgnd /VEE 100n
* preamp -- 4x+ balanced gain
XU_preamp T5-AA_ U10A--IN_A_ U10A-+IN_A_ /VCC U10B-+IN_B_ U10B--IN_B_ T6-AA_ /VEE OPAMP_DIP8

*in
R23 /Unbal- U10A-+IN_A_ 10k
R21 /Unbal+ U10B-+IN_B_ 10k

R25 U10A-+IN_A_ /Vgnd 10k
R20 U10B-+IN_B_ /Vgnd 10k

* feedback
R26 U10A--IN_A_ T5-AA_ 10k
R6 U10B--IN_B_ T6-AA_ 10k

R22 /Unbal+ U10B--IN_B_ 500
R24 /Unbal- U10A--IN_A_ 500

* out
C1 T6-AA_ T5-AA_ .1u

XT5 T5-AA_ /Vgnd /PRE+ GND XFMR
XT6 T6-AA_ /Vgnd /PRE- GND XFMR





*balanced power
Cbal_e1 /Vgnd /VEE 100n
Cbal_c1 /Vgnd /VCC 100n

* RCA to balanced -- goal is unity gain
X_bal bal-OUT_A_ bal--IN_A_ bal-+IN_A_ /VCC /Vgnd bal--IN_B_ bal-out_B_ /VEE OPAMP_DIP8

* in+ (noninverting)
Rbal_in1 bal-+IN_A_ IN 10k
Rbal_in_gnd1 bal-+IN_A_ /Vgnd 10k
* feedback+
Rbal_back1 bal--IN_A_ bal-OUT_A_ 10k
Rbal_back_gnd1 bal--IN_A_ /Vgnd 10k

* in - (inverting)
Rbal_in2 bal--IN_B_ IN 10k
* feedback-
Rbal_back2 bal--IN_B_ bal-OUT_B_ 10k

* out
Rbal_out1 bal-OUT_A_ /Unbal+ 300
Cbal1 /Vgnd_ bal-OUT_A_ .1u

Rbal_out2 bal-OUT_B_ /Unbal- 300
Cbal2 /Vgnd bal-OUT_B_ .1u

**********************to do filter

********************** tube amp
* input
R2 /B+ U3B-A_ 260k
R3 /B+ U3A-A_ 260k
XU3 U3B-A_ /PRE+ U3B-K_ /H1 /H2 U3A-A_ /PRE- U3A-K_ /HGND_12ax7 12AX7_dual
* output via Annode
C3 U3B-A_ /SIG+ 0.22u ; 400V Bypass caps
C4 U3A-A_ /SIG- 0.22u ; 400V Bypass caps

* was 220k -- higher R is higher voltage /SIG on GRID going into KT88
Rgrid1 /SIG+ GND 220k
Rgrid2 /SIG- GND 220k

* Cathode bypass caps
C6 GND U3B-K_ 25u
R5 U3B-K_ GND 1.5k

C7 GND U3A-K_ 25u
R4 U3A-K_ GND 1.5k

* output
Rstop1 G1A /SIG+ 1k
* KT88 is now toggling -- swaped out lm317 model, redid bias resistors, found K and G2 swapped in KT88 model
* spice shows bogus 400V-1.5kV as well as bogus0--500v, so /Spk+ not showing right output (expect big sin wave) -- but this is likely due to these 2 false simulation artifacts
* FALSE sim -- above and below plate voltage
* also have TI opamp model working, so close to .FFT & design work
* possible my issue is the hand off from opam to 12ax7 --opamp +/-9V, 12ax7 0-230V; while looks sane, sim maybe missing something and I don't know
XU5 NC-U5-0 G1A U5A-K_G3_ /H3 /HGND_KT88 NC-U5-1 out_p NC-U5-2 /l+ KT88

Rstop2 G1B /SIG- 1k
XU6 NC-U6-0 G1B U6A-K_G3_ /H4 /HGND_KT88 NC-U6-1 out_n NC-U6-2 /l- KT88

* bias
XU7 U7-ADJ_ GND U7-VI_ LM317
*********************Rset VI cathode 10    ; Sets I = 1.25 / 10 = 125â€¯mA
R1 U7-ADJ_ GND 36

R8 U7-VI_ U6A-K_G3_ 4
R7 U5A-K_G3_ U7-VI_ 4

* power bypas cap
C2 GND /B+ 100u ; 500V Jupiter Axial Cap
XT4 out_p /l+ /B+ /l- out_n unconnected-_T4-Pad6_ /Spk+ unconnected-_T4-Pad8_ /Spk- Ham1650P
* measure output here
* LS1 /Spk- /Spk+ Speaker
RSPK_common1 /Spk- GND 1




.tran 10u 20m
.control
run
plot v(/PRE+) v(/PRE-) v(g1a) v(g1b) v(/Spk+) v(/Spk-)
.endc
* ===========================
* 12AX7 SPICE Model by Norman Koren
* ===========================
.SUBCKT 12AX7_single 1 2 3  ; P G C;  NEW MODEL
+ PARAMS: MU=100 EX=1.4 KG1=1060 KP=600 KVB=300 RGI=2000 
+ CCG=2.3P  CGP=2.4P CCP=.9P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE={V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID
C2 2 1 {CGP}  ; GRID=PLATE
C3 1 3 {CCP}  ; CATHODE-PLATE
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 12AX7_dual 1 2 3 4 5 6 7 8 9
* pin 4 id H1
* pin 5 is H2
* pin 9 is HGND
XUB 1 2 3 12AX7_single
XUA 6 7 8 12AX7_single
.ENDS 12AX7_dual

* ===========================
* KT88 SPICE Model
* ===========================
*.SUBCKT KT88 1 2 3 4 ;  P G1 C G2
* pin 1,6,8 unused
.SUBCKT KT88 nc 3 2 heater+ heater- nc 1 nc 4
+ PARAMS: MU=8.8 EX=1.35 KG1=730 KG2=4800 KP=32 KVB=16
+         CCG=14P CPG1=.85P CCP=12P RGI=1K 

RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE={V(4,3)/KP*LOG(1+EXP((1/MU+V(2,3)/V(4,3))*KP))}
G1   1 3  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,3)/KVB)}
* Safe, clamped version of G2 to avoid log(negative)
G2   4 3  VALUE={(EXP(EX*LOG(LIMIT((V(4,3)/MU)+V(2,3), 1e-6, 1e6))))/KG2}

RCP  1 3  1G      ; FOR CONVERGENCE
C1   2 3  {CCG}    ; CATHODE-GRID 1
C2   1 2  {CPG1}  ; GRID 1-PLATE
C3   1 3  {CCP}   ; CATHODE-PLATE
R1   2 5  {RGI}   ; FOR GRID CURRENT
D3   5 3  DX      ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS KT88

* ===========================
* isolation transformer
* ===========================
.SUBCKT XFMR 1 2 3 4
L1 1 2 1000
L2 3 4 1000
K1 L1 L2 .9999999
.ENDS

* ===========================
* output transformer
* ===========================
.SUBCKT Ham1650P P1 Sg1 B Sg2 P2 O16 O8 O4 Com				
* Push Pull transformer, with Ultralinear taps at 40%				
* 6600 to 16 ohms, 3db 15 to 60000 hz				
* speaker taps at 8 and 4 ohms				
*  Hammond 1650P				
*   				
*  model generated by TransformerModels.xls 2025 May 11				
*  				
LP1 1 Sg1 3.19344114792738				
LS1 2 B 1.41930717685661				
LS2 3 Sg2 1.41930717685661				
LP2 4 P2 3.19344114792738				
LA1 5 O8 0.00741645776380232				
LA2 6 O4 0.00370822888190116				
LA3 7 Com 0.02161314180004				
KALL LP1 LS1 LS2 LP2 LA1 LA2 LA3 0.998434750532355				
RP1 P1 1 30				
RP2 Sg1 2 15				
RP3 B 3 15				
RP4 Sg2 4 30				
RS1 O16 5 0.1				
RS2 O8 6 0.1				
RS3 O4 7 0.1				
.ENDS Ham1650P				
* Acro Elco HK Peerless Dynaco Leak Gen Edcor

* ===========================
* LM317 -- BIAS (so all about the current extration -- 0 = (1 +R2/R1)*1.25
* ===========================
.SUBCKT LM317 1    2        3
*Connections  Adj. Output   Input
*LM317 voltage regulator - Texas Instruments
J1 1 3 4 JN
Q2 5 5 6 QPL .1
Q3 5 8 9 QNL  .2
Q4 8 5 7 QPL .1
Q5 81 8 3 QNL .2
Q6 3 81 10 QPL .2
Q7 12 81 13 QNL  .2
Q8 10 5 11 QPL  .2 
Q9 14 12 10 QPL .2
Q10 16 5 17 QPL  .2
Q11 16 14 15 QNL .2
Q12 3 20 16 QPL .2
Q13 1 19 20 QNL .2
Q14 19 5 18 QPL .2
Q15 3 21 19 QPL .2
Q16 21 22 16 QPL .2
Q17 21 3 24 QNL   .2
Q18 22 22 16 QPL .2
Q19 22 3 241 QNL 2
Q20 3 25 16 QPL .2
Q21 25 26 3 QNL .2
Q22A 35 35 1 QPL 2
Q22B 16 35 1 QPL 2
Q23 35 16 30 QNL  2
Q24A 27 40 29 QNL .2
Q24B 27 40 28 QNL .2
Q25 1 31 41 QNL 5
Q26 1 41 32 QNL 50
D1 3 4 DZ
D2 33 1 DZ
D3 29 34 DZ
R1 1 6 310
R2 1 7 310
R3 1 11 190
R4 1 17 82
R5 1 18 5.6K
R6 4 8 100K
R7 8 81 130
R8 10 12 12.4K
R9 9 3 180
R10 13 3 4.1K
R11 14 3 5.8K
R12 15 3 72
R13 20 3 5.1K
R14 2 24 12K
R15 24 241 2.4K
R16 16 25 6.7K
R17 16 40 12K
R18 30 41 130
R19 16 31 370
R20 26 27 13K
R21 27 40 400
R22 3 41 160
R23 33 34 18K
R24 28 29 160
R25 28 32 3
R26 32 3 .1
C1 21 3 30PF
C2 21 2 30PF
C3 25 26 5PF
CBS1 5 3 2PF
CBS2 35 3 1PF
CBS3 22 3 1PF
.MODEL JN NJF(BETA=1E-4 VTO=-7)
.MODEL DZ D(BV=6.3)
.MODEL QNL NPN(EG=1.22 BF=80 RB=100 CCS=1.5PF TF=.3NS TR=6NS CJE=2PF
+ CJC=1PF VAF=100)
.MODEL QPL PNP(BF=40 RB=20 TF=.6NS TR=10NS CJE=1.5PF CJC=1PF VAF=50)
.ENDS LM317

********************************************************************************
* opaamp using old, but eventually want TI OPA2156
* Is a DIP8 Package
********************************************************************************
.SUBCKT OPAMP_DIP8 1 2 3 4 5 6 7 8
X_opamp_A 3 2 4 8 1 OPAMP_SINGLE
X_opamp_B 5 6 4 8 7 OPAMP_SINGLE
.ENDS

.end
