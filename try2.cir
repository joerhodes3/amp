.title modern_tube_amp
*
* old opamp -- remove and include TI in compatibility mode
.include "/home/jrhodes/modern_tube_amp/spice/opamp_old.lib"
*
*.include "/home/jrhodes/modern_tube_amp/spice/opa1641.lib"
*.include "/home/jrhodes/modern_tube_amp/spice/OPA2156.lib"
*
* input -- RCA and/or XLR+ (pin 2)
*V1 IN GND DC SIN(0 1.2 1k)
V1 IN GND DC 0 AC 0 SIN(0 1.2 1k)
* in truth GND and GND will likely differ by 12V or so -- that is why they are isolated
* XLR- (pin 3)
*V2 IN- GND DC -V(IN) ; should have worked

* board power -- export always on for UI, make 5V switch which is only for selector (RCA/XLR)
V3 /pwr GND DC 5

* op amp power
V4 /VEE GND DC -15
V5 /VCC GND DC 15

* tube heaters -- make soft start, RC delay ramps up slowly?
V6 /H1 /HGND_1 DC 6 
V7 /H2 /HGND_2 DC 6 

* tube power (plate)
V8 /B+ GND DC 400
* grid bias
V9 /GridBias GND -50

* GND management for sim -- don't do in real life
Rsim1 GND 0 1
Rsim2 /HGND_1 0 1
Rsim3 /HGND_2 0 1

*********************** ss pre amp
* op pwr
C23 GND /VCC 330u ; Tantalum
C24 /VEE GND 330u ; Tantalum
C25 GND /VCC 330u ; Tantalum
C26 /VEE GND 330u ; Tantalum
C27 GND /VCC 330u ; Tantalum
C28 /VEE GND 330u ; Tantalum

* preamp power
C19 GND /VCC 100n
C20 GND /VEE 100n
* preamp -- 6x+ balanced gain (+/-12V)
XU_preamp OUTA+ U10A--IN_A_ U10A-+IN_A_ /VCC U10B-+IN_B_ U10B--IN_B_ OUTB- /VEE OPAMP_DIP8

*in
R23 /Unbal- U10A-+IN_A_ 10k
R21 /Unbal+ U10B-+IN_B_ 10k

R25 U10A-+IN_A_ GND 1.5k
R20 U10B-+IN_B_ GND 1.5k

* feedback
R26 U10A--IN_A_ OUTA+ 22k
R6 U10B--IN_B_ OUTB- 22k

R22 /Unbal+ U10B--IN_B_ 1.5k
R24 /Unbal- U10A--IN_A_ 1.5k

* out
Csignal1 OUTB- OUTA+ 0.1u

Rpre_out1 /PRE+ OUTA+ 4.7k
Rpre_out2 /PRE- OUTB- 4.7k





*balanced power
Cbal_e1 GND /VEE 100n
Cbal_c1 GND /VCC 100n

* RCA to balanced -- goal is unity gain
X_bal bal-OUT_A_ bal--IN_A_ bal-+IN_A_ /VCC GND bal--IN_B_ bal-out_B_ /VEE OPAMP_DIP8

* in+ (noninverting)
Rbal_in1 bal-+IN_A_ IN 10k
Rbal_in_gnd1 bal-+IN_A_ GND 10k
* feedback+
Rbal_back1 bal--IN_A_ bal-OUT_A_ 10k
Rbal_back_gnd1 bal--IN_A_ GND 10k

* in - (inverting)
Rbal_in2 bal--IN_B_ IN 10k
* feedback-
Rbal_back2 bal--IN_B_ bal-OUT_B_ 10k

* out
Rbal_out1 bal-OUT_A_ /Unbal+ 500
Cbal1 GND bal-OUT_A_ .1u

Rbal_out2 bal-OUT_B_ /Unbal- 500
Cbal2 bal-OUT_B_ GND .1u

**********************to do filter


********************** tube amp
* === NMOS Followers ===
XMDRIVE_A DRAIN_A /PRE+ SRC_A ZVN4310
XMDRIVE_B DRAIN_B /PRE- SRC_B ZVN4310

* Gate input bias 60v
RIN_lo_A /PRE+ GND 400k
RIN_lo_B /PRE- GND 400k
RIN_hi_A /PRE+ /B+ 60k
RIN_hi_B /PRE- /B+ 60k

* NMOS Drain -- output here nor S???
RDRAIN_hi_A /B+ DRAIN_A 47k ; 3W -- https://hoffmanamps.com/
RDRAIN_hi_B /B+ DRAIN_B 47k ; 3W -- https://hoffmanamps.com/
* follower output caps to remove DC from NMOS stage
COUT_A DRAIN_A /SIG+ 0.1u ; Panasonic-104 -- https://hoffmanamps.com/
COUT_B DRAIN_B /SIG- 0.1u ; 250V Bypass caps -- https://hoffmanamps.com/

* Source
RSRC_A SRC_A GND 4.7k ; 3W -- https://hoffmanamps.com/
RSRC_B SRC_B GND 4.7k ; 3W -- https://hoffmanamps.com/


* === Grid-leak resistors to bias point for G1 (-100v) ===
RGB1A /SIG+ /GridBias 22k ; 3W -- https://hoffmanamps.com/
RGB1B /SIG- /GridBias 22k ; 3W -- https://hoffmanamps.com/
CGB_1 /GridBias GND 16u ; FT 475v -- https://hoffmanamps.com/

* === KT88 Connections ===
* bias cathode -- VI is not constant 42V+
XU7 U7-ADJ_ GND U7-VI_ LM317
R1 U7-ADJ_ GND 22

R8 KT88A_K U7-VI_ 250 ; 5W :: wirewound -- https://hoffmanamps.com/
R7 KT88B_K U7-VI_ 250 ; 5W :: wirewound -- https://hoffmanamps.com/

C8 GND KT88A-K 25u
C9 GND KT88B-K 25u

* === Ultralinear screen grid ===
* Assuming ideal TX with taps L+ and L-
RLG2A /l+ KT88A_G2 1k ; 3W -- https://hoffmanamps.com/
RLG2B /l- KT88B_G2 1k ; 3W -- https://hoffmanamps.com/
* STOP resistors for G1
RG1A /SIG+ KT88A_G1 4.7k ; 3W -- https://hoffmanamps.com/
RG1B /SIG- KT88B_G1 4.7k ; 3W -- https://hoffmanamps.com/
* Tubes (Push-Pull)
* Remember 8pin sockets -- https://hoffmanamps.com/
XKT88A NC-U5-0 KT88A_G1 KT88A_K /H1 /HGND_1 NC-U5-1 out_p NC-U5-2 KT88A_G2 KT88
XKT88B NC-U6-0 KT88B_G1 KT88B_K /H2 /HGND_2 NC-U6-1 out_n NC-U6-2 KT88B_G2 KT88

* power bypas cap
C2 GND /B+ 47u ; 500V FT
XT4 out_p /l+ /B+ /l- out_n unconnected-_T4-Pad6_ /Spk+ unconnected-_T4-Pad8_ /Spk- Ham1650P
* measure output here
Rload /Spk+ /Spk- 8
* for sim
RSPK_common1 /Spk- GND 1




.IC V(/PRE+)=0
.IC V(/PRE-)=0
.IC V(/GridBias)=-36
.control
set noaskquit
**
** Manual sweep for frequency and THD calculation (in place of .four if unavailable)
foreach f 1k
**foreach f 15 22 33 50 75 100 150 220 330 500 750 1k 1500 2200 3300 5000 7500 10000 15000 22000 25000
    alter V1 SIN(0 1.2 $f)
*
    tran 10u 50m
*
    echo "Frequency $f Hz complete" > sweep_$f
*
**     fourier $f 10m 20m v(/spk+)
**     display fourier*
**    wrdata raw_sweep_$f v(/Spk+)
**    let idx = 2
**    let sum_mag_square = 0
**    while idx < 10
**        let mag = fourier11[1][$&idx]
**        let sum_mag_square = sum_mag_square + mag * mag
**        let idx = $&idx + 1
**    end
**    let root_sum_mag_square = sqrt(sum_mag_square)
**    let thd = root_sum_mag_square / fourier11[1][1] * 100
*
    meas TRAN Vrms_load RMS V(/Spk+) from=10m to=20m
    let Power_out = ($&Vrms_load*$&Vrms_load)/8
*
    echo "RMS Vout = $&Vrms_load V" >> sweep_$f
    echo "Power_out = $&Power_out W" >> sweep_$f
**    echo "THD = $&thd" >> sweep_$f
end
run
*plot v(/Unbal+) v(/Unbal-) v(OUTA+) v(OUTB-) v(/PRE+) v(/PRE-)
**plot v(/PRE+) v(/PRE-) v(DRAIN_A) v(SRC_A) v(/SIG+) v(/SIG-)
*plot v(IN) v(/Spk+) v(U7-VI_)
*plot v(out_p) v(out_n)
**plot v(/PRE+) v(/PRE-) v(KT88A_G1) - v(KT88A_k) v(KT88B_G1) - v(KT88B_K) v(/SIG+) v(/SIG-)  v(out_p) v(out_n) v(/Spk+)
plot v(KT88A_G1) v(KT88B_G1) v(KT88A_K) v(out_p) v(out_n) v(/Spk+)
**plot v(KT88A_G1) v(KT88B_G1) v(out_p) v(out_n) v(/Spk+) v(/Spk-)
**plot v(/l+) v(/l-) v(KT88A_G1) - v(KT88A_k) v(KT88B_G1) - v(KT88B_K) v(out_p) v(out_n)



*.op
*.control
*run
*print V(VDRAIN_B)
*print V(GBUF_B)
*print V(/SIG-)
*print V(G1B)
*print V(U6A-K_G3_)
*print V(G1B) - V(U6A-K_G3_)
* ngspice:
*v(vdrain_b) = 6.911437e+01
*v(gbuf_b) = 5.883718e+01
*v(/sig-) = 1.063171e+01
*v(g1b) = 1.063172e+01
*v(u6a-k_g3_) = 5.378549e+01
* goal is -45v to -52v diif grid to cathode?
*v(g1b) - v(u6a-k_g3_) = -4.31538e+01
*
.endc

* ===========================
* KT88 SPICE Model
* ===========================
*.SUBCKT KT88 1 2 3 4 ;  P G1 C G2
* pin 1,6,8 unused
.SUBCKT KT88 nc 2 3 heater+ heater- nc 1 nc 4
+ PARAMS: MU=8.8 EX=1.35 KG1=730 KG2=4800 KP=32 KVB=16
+         CCG=14P CPG1=.85P CCP=12P RGI=1K 

RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE={V(4,3)/KP*LOG(1+EXP((1/MU+V(2,3)/V(4,3))*KP))}
G1   1 3  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,3)/KVB)}
* Safe, clamped version of G2 to avoid log(negative)
G2   4 3  VALUE={(EXP(EX*LOG(LIMIT((V(4,3)/MU)+V(2,3), 1e-6, 1e6))))/KG2}

RCP  1 3  1G      ; FOR CONVERGENCE
C1   2 3  {CCG}    ; CATHODE-GRID 1
C2   1 2  {CPG1}  ; GRID 1-PLATE
C3   1 3  {CCP}   ; CATHODE-PLATE
R1   2 5  {RGI}   ; FOR GRID CURRENT
D3   5 3  DX      ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS KT88

* ===========================
* isolation transformer
* ===========================
.SUBCKT XFMR 1 2 3 4
L1 1 2 1000
L2 4 3 1000
K1 L1 L2 .9999999
.ENDS

* ===========================
* output transformer
* ===========================
.SUBCKT Ham1650P out_p g2a /B+ g2b out_n unconnected-_T4-Pad6_ /Spk+ unconnected-_T4-Pad8_ /Spk-
* === Output Transformer Model ===
* Primary windings
Lpri1 out_p pri_ct 10H
Lpri2 out_n pri_ct 10H

* Secondary winding
Lsec /Spk- /Spk+ 100mH

* Mutual coupling
Kout Lpri1 Lpri2 Lsec 0.995

* Center tap to B+
Rpri_ct1 /B+ pri_ct 1 ; << This connection is critical

Eultra1 g2a 0 VALUE = { V(out_p) * 0.4 + V(/B+) * 0.6 }
Eultra2 g2b 0 VALUE = { V(out_n) * 0.4 + V(/B+) * 0.6 }
.ENDS Ham1650P				

.SUBCKT Ham1650P_xls P1 Sg1 B Sg2 P2 O16 O8 O4 Com				
* Push Pull transformer, with Ultralinear taps at 40%				
* 6600 to 16 ohms, 3db 15 to 60000 hz				
* speaker taps at 8 and 4 ohms				
*  Hammond 1650P				
*   				
*  model generated by TransformerModels.xls 2025 May 11				
*  				
LP1 1 Sg1 3.19344114792738				
LS1 2 B 1.41930717685661				
LS2 3 Sg2 1.41930717685661				
LP2 4 P2 3.19344114792738				
LA1 5 O8 0.00741645776380232				
LA2 6 O4 0.00370822888190116				
LA3 7 Com 0.02161314180004				
KALL LP1 LS1 LS2 LP2 LA1 LA2 LA3 0.998434750532355				
RP1 P1 1 30				
RP2 Sg1 2 15				
RP3 B 3 15				
RP4 Sg2 4 30				
RS1 O16 5 0.1				
RS2 O8 6 0.1				
RS3 O4 7 0.1				
.ENDS Ham1650P_xls		
* Acro Elco HK Peerless Dynaco Leak Gen Edcor

* ===========================
* LM317 -- BIAS (so all about the current extration -- 0 = (1 +R2/R1)*1.25
* ===========================
.SUBCKT LM317 1    2        3
*Connections  Adj. Output   Input
*LM317 voltage regulator - Texas Instruments
J1 1 3 4 JN
Q2 5 5 6 QPL .1
Q3 5 8 9 QNL  .2
Q4 8 5 7 QPL .1
Q5 81 8 3 QNL .2
Q6 3 81 10 QPL .2
Q7 12 81 13 QNL  .2
Q8 10 5 11 QPL  .2 
Q9 14 12 10 QPL .2
Q10 16 5 17 QPL  .2
Q11 16 14 15 QNL .2
Q12 3 20 16 QPL .2
Q13 1 19 20 QNL .2
Q14 19 5 18 QPL .2
Q15 3 21 19 QPL .2
Q16 21 22 16 QPL .2
Q17 21 3 24 QNL   .2
Q18 22 22 16 QPL .2
Q19 22 3 241 QNL 2
Q20 3 25 16 QPL .2
Q21 25 26 3 QNL .2
Q22A 35 35 1 QPL 2
Q22B 16 35 1 QPL 2
Q23 35 16 30 QNL  2
Q24A 27 40 29 QNL .2
Q24B 27 40 28 QNL .2
Q25 1 31 41 QNL 5
Q26 1 41 32 QNL 50
D1 3 4 DZ
D2 33 1 DZ
D3 29 34 DZ
R1 1 6 310
R2 1 7 310
R3 1 11 190
R4 1 17 82
R5 1 18 5.6K
R6 4 8 100K
R7 8 81 130
R8 10 12 12.4K
R9 9 3 180
R10 13 3 4.1K
R11 14 3 5.8K
R12 15 3 72
R13 20 3 5.1K
R14 2 24 12K
R15 24 241 2.4K
R16 16 25 6.7K
R17 16 40 12K
R18 30 41 130
R19 16 31 370
R20 26 27 13K
R21 27 40 400
R22 3 41 160
R23 33 34 18K
R24 28 29 160
R25 28 32 3
R26 32 3 .1
C1 21 3 30PF
C2 21 2 30PF
C3 25 26 5PF
CBS1 5 3 2PF
CBS2 35 3 1PF
CBS3 22 3 1PF
.MODEL JN NJF(BETA=1E-4 VTO=-7)
.MODEL DZ D(BV=6.3)
.MODEL QNL NPN(EG=1.22 BF=80 RB=100 CCS=1.5PF TF=.3NS TR=6NS CJE=2PF
+ CJC=1PF VAF=100)
.MODEL QPL PNP(BF=40 RB=20 TF=.6NS TR=10NS CJE=1.5PF CJC=1PF VAF=50)
.ENDS LM317

********************************************************************************
* opaamp using old, but eventually want TI OPA2156
* Is a DIP8 Package
********************************************************************************
.SUBCKT OPAMP_DIP8 1 2 3 4 5 6 7 8
X_opamp_A 3 2 4 8 1 OPAMP_SINGLE
X_opamp_B 5 6 4 8 7 OPAMP_SINGLE
.ENDS

********************************************************************************
* ZVN4310GTA --N-CHANNEL MOSFET SOR 223 package? 31-99V???
********************************************************************************
*ZETEX ZVN4310GTA Spice Model v1.1 Last Revised 8/2/02
.SUBCKT ZVN4310 3 4 5
*NODES: DRAIN GATE SOURCE
M1 3 2 5 5 MOD1
RG 4 2 82
RL 3 5 1E7
C1 2 5 200E-12
C2 3 2 13.5E-12 
D1 5 3 DIODE1
*
.MODEL MOD1 NMOS VTO=2.555 RS=0.2068 RD=0.1665 IS=1E-15 KP=5.02
+CBD=288E-12 PB=1 LAMBDA=0
.MODEL DIODE1 D IS=9.759E-13 N=1.02476 RS=0.0844
.ENDS ZVN4310A

.end
