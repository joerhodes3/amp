.title modern_tube_amp
*
*figure 2 XPA6 from emotiva is $4400.....so
*
*budget $340 amp
*  PCB: 50
*  power: 75 [with 2 heat]
*  amp: 75 [with 2 heat]
*  tans: 45
*  power: 70
*  -----------
*  $315
*
*+ $320 custom woodwork
*....for 12 amps
*
* old opamp -- remove and include TI in compatibility mode
.include "/home/jrhodes/modern_tube_amp/spice/opamp_old.lib"
*
*.include "/home/jrhodes/modern_tube_amp/spice/opa1641.lib"
*.include "/home/jrhodes/modern_tube_amp/spice/OPA2156.lib"
*
* input -- RCA and/or XLR+ (pin 2)
*V1 IN GND DC SIN(0 1.2 1k)
V1 IN GND DC 0 AC 0 SIN(0 1.3 1k)
* in truth GND and GND will likely differ by 12V or so -- that is why they are isolated
* XLR- (pin 3)
*V2 IN- GND DC -V(IN) ; should have worked

* ================================== Power(TODO -- ideal for now)
* input cap
Cinput_1 /GridBias GND 4700u ; power 4700u, this 1k or 2k -- nichicron?

* TODO
*                            |---relay-------------------------------------------------------------|------------------- offset -------
*          |--- +48v ---|----|----TIP141--|---------------------------------|-- +24v --|           |
*          |            |                 |                                 |          |           |
*          |            |                 |                                 |          +15v        |
*          |            |                 |                                 |          |           |
*          |            |                 +12v (LDO)                        |          |           |
*          |            5V - (LDO)        switched                          |          |           |
*48V, 10A--|            always on         |----TIP141---->trigger out       |-- vGND --|           |
*          |            |                 |                                 |          |           /B+ = 200v 1.7a
*          |            |                 |                                 |          |           |
*          |            |                 |                                 |          -15v        |
*          |            |                 |                                 |          |           |
*          |            |                 |                                 |-- -24v --|           |
*          |--- GND-----|-----------------|--------------------------------------------------------|---------------------------------------

* ideal for now
V3 /pwr GND DC 5

* op amp power
V4 /VEE GND DC -15
V5 /VCC GND DC 15

* op amp power caps
C23 GND /VCC 330u ; Tantalum
C24 /VEE vGND 330u ; Tantalum
C25 GND /VCC 330u ; Tantalum
C26 /VEE vGND 330u ; Tantalum

* output power
V8 /B+ GND DC 200
* grid bias
V9 /GridBias GND 48

* GND management for sim -- don't do in real life
Rsim1 GND 0 1
Rsim2 vGND_1 0 24

* ================================== RCA to balanced
*balanced power
Cbal_e1 vGND /VEE 100n
Cbal_c1 vGND /VCC 100n

* RCA to balanced -- goal is unity gain
X_bal bal-OUT_A_ bal--IN_A_ bal-+IN_A_ /VCC GND bal--IN_B_ bal-out_B_ /VEE OPAMP_DIP8

* in+ (noninverting)
Rbal_in1 bal-+IN_A_ IN 10k
Rbal_in_gnd1 bal-+IN_A_ GND 10k
* feedback+
Rbal_back1 bal--IN_A_ bal-OUT_A_ 10k
Rbal_back_gnd1 bal--IN_A_ GND 10k

* in - (inverting)
Rbal_in2 bal--IN_B_ IN 10k
* feedback-
Rbal_back2 bal--IN_B_ bal-OUT_B_ 10k

* out
Rbal_out1 bal-OUT_A_ /Unbal+ 500
Cbal1 vGND bal-OUT_A_ .1u

Rbal_out2 bal-OUT_B_ /Unbal- 500
Cbal2 bal-OUT_B_ vGND .1u

* ================================== Pre amp (opamp +/-1v --> +/-10v)
* preamp power
C19 vGND /VCC 100n
C20 vGND /VEE 100n
* preamp -- 6x+ balanced gain (+/-12V)
XU_preamp OUTA+ U10A--IN_A_ U10A-+IN_A_ /VCC U10B-+IN_B_ U10B--IN_B_ OUTB- /VEE OPAMP_DIP8

*in
R23 /Unbal- U10A-+IN_A_ 10k
R21 /Unbal+ U10B-+IN_B_ 10k

R25 U10A-+IN_A_ vGND 1.5k
R20 U10B-+IN_B_ vGND 1.5k

* feedback
R26 U10A--IN_A_ OUTA+ 22k
R6 U10B--IN_B_ OUTB- 22k

R22 /Unbal+ U10B--IN_B_ 1.5k
R24 /Unbal- U10A--IN_A_ 1.5k

* out
Csignal1 OUTB- OUTA+ 0.1u

Rpre_out1 /PRE+ OUTA+ 4.7k
Rpre_out2 /PRE- OUTB- 4.7k

* ================================== NMOS buffer vGND --> GND (The sim is relative to this) also is stronger and is a 50v swing
* NMOS Followers
XMDRIVE_A DRAIN_A /PRE+ SRC_A ZVN4310
XMDRIVE_B DRAIN_B /PRE- SRC_B ZVN4310

* Gate input bias relative to vGND 200*(60/470)=26v --> +/-10+24+26 = 40v to 60v to Gate 
RIN_lo_A /PRE+ GND 470k
RIN_lo_B /PRE- GND 470k
RIN_hi_A /PRE+ /B+ 56k
RIN_hi_B /PRE- /B+ 56k

* NMOS Drain
RDRAIN_hi_A /B+ DRAIN_A 47k ; 3W -- https://hoffmanamps.com/
RDRAIN_hi_B /B+ DRAIN_B 47k ; 3W -- https://hoffmanamps.com/

* Source
RSRC_A SRC_A GND 4.7k ; 3W -- https://hoffmanamps.com/
RSRC_B SRC_B GND 4.7k ; 3W -- https://hoffmanamps.com/

* ================================== filter [improve]
* follower output caps to remove DC from NMOS stage
COUT_A DRAIN_A /SIG+ 0.1u ; Panasonic-104 -- https://hoffmanamps.com/
COUT_B DRAIN_B /SIG- 0.1u ; 250V Bypass caps -- https://hoffmanamps.com/

RGB1A /SIG+ /GridBias 27k ; 3W -- https://hoffmanamps.com/
RGB1B /SIG- /GridBias 27k ; 3W -- https://hoffmanamps.com/
Coffset_1 /GridBias GND 2000u ; power 4700u, this 1k or 2k -- nichicron?

* ================================== output
Rh1 /SIG+ /out_hi1 4.7k ; 3W -- https://hoffmanamps.com/
XOut_hi1 /B+ /out_hi1 out_p R6535KNZ
Rh2 /SIG+ /out_hi2 4.7k ; 3W -- https://hoffmanamps.com/
XOut_hi2 /B+ /out_hi2 out_p R6535KNZ
Rh3 /SIG+ /out_hi3 4.7k ; 3W -- https://hoffmanamps.com/
XOut_hi3 /B+ /out_hi3 out_p R6535KNZ

Rl1 /SIG- /out_lo1 4.7k ; 3W -- https://hoffmanamps.com/
XOut_lo1 /B+ /out_lo1 out_n R6535KNZ
Rl2 /SIG- /out_lo2 4.7k ; 3W -- https://hoffmanamps.com/
XOut_lo2 /B+ /out_lo2 out_n R6535KNZ
Rl3 /SIG- /out_lo3 4.7k ; 3W -- https://hoffmanamps.com/
XOut_lo3 /B+ /out_lo3 out_n R6535KNZ

* power bypas cap
C200_a GND /B+ 680u ; 200V nichicon
C200_b GND /B+ 680u ; 200V nichicon
XT4 out_p GND out_n /Spk+ /Spk- Toroid
* measure output here
Rload /Spk+ /Spk- 8
* for sim
RSPK_common1 /Spk- GND 1

* ===========================================================================

.IC V(/PRE+)=0
.IC V(/PRE-)=0
.IC V(/GridBias)=-36
.control
set noaskquit
**
** Manual sweep for frequency and THD calculation (in place of .four if unavailable)
foreach f 1k
**foreach f 15 22 33 50 75 100 150 220 330 500 750 1k 1500 2200 3300 5000 7500 10000 15000 22000 25000
    alter V1 SIN(0 1.2 $f)
*
    tran 10u 50m
*
    echo "Frequency $f Hz complete" > sweep_$f
*
**     fourier $f 10m 20m v(/spk+)
**     display fourier*
**    wrdata raw_sweep_$f v(/Spk+)
**    let idx = 2
**    let sum_mag_square = 0
**    while idx < 10
**        let mag = fourier11[1][$&idx]
**        let sum_mag_square = sum_mag_square + mag * mag
**        let idx = $&idx + 1
**    end
**    let root_sum_mag_square = sqrt(sum_mag_square)
**    let thd = root_sum_mag_square / fourier11[1][1] * 100
*
    meas TRAN Vrms_load RMS V(/Spk+) from=10m to=20m
    let Power_out = ($&Vrms_load*$&Vrms_load)/8
*
    echo "RMS Vout = $&Vrms_load V" >> sweep_$f
    echo "Power_out = $&Power_out W" >> sweep_$f
**    echo "THD = $&thd" >> sweep_$f
end
run
*plot v(/IN) v(/Unbal+) v(/Unbal-)
**plot v(/PRE+) v(/PRE-) v(DRAIN_A) v(SRC_A) v(/SIG+) v(/SIG-)
plot v(/PRE+) v(/PRE-) v(DRAIN_A) v(DRAIN_B) v(out_p) v(out_n)



*.op
*.control
*run
*print V(VDRAIN_B)
*print V(GBUF_B)
*print V(/SIG-)
*print V(G1B)
*print V(U6A-K_G3_)
*print V(G1B) - V(U6A-K_G3_)
* ngspice:
*v(vdrain_b) = 6.911437e+01
*v(gbuf_b) = 5.883718e+01
*v(/sig-) = 1.063171e+01
*v(g1b) = 1.063172e+01
*v(u6a-k_g3_) = 5.378549e+01
* goal is -45v to -52v diif grid to cathode?
*v(g1b) - v(u6a-k_g3_) = -4.31538e+01
*
.endc

* ===========================
* isolation transformer
* ===========================
.SUBCKT XFMR 1 2 3 4
L1 1 2 1000
L2 4 3 1000
K1 L1 L2 .9999999
.ENDS

* ===========================
* output transformer
* ===========================
* https://www.antekinc.com/au-2258-200va-58v-transformer/

.SUBCKT Toroid blue1 green blue2 red black
* Transformer model with winding resistance and mutual inductance
Rpri1 blue1 green 3.3       ; Primary winding resistance (half)
Rpri2 blue2 green 3.3       ; Primary winding resistance (half)
Rsec  red black 1           ; Secondary winding resistance (center tapped)

* any L kills output, but i need it to get K
*Lpri1 blue1 green .1m      ; Half-primary inductance
*Lpri2 blue2 green .1m
*Lsec  red black   .2m      ; Full secondary inductance

*K1 Lpri1 Lpri2 Lsec 0.9999 ; Tight coupling

.ENDS
*
* https://edcorusa.com/products/cxpp100-5k-100w-5k-ohms-push-pull-tube-output-transformer
*     or Ham1650P

********************************************************************************
* opaamp using old, but eventually want TI OPA2156
* Is a DIP8 Package
********************************************************************************
.SUBCKT OPAMP_DIP8 1 2 3 4 5 6 7 8
X_opamp_A 3 2 4 8 1 OPAMP_SINGLE
X_opamp_B 5 6 4 8 7 OPAMP_SINGLE
.ENDS

********************************************************************************
* ZVN4310GTA --N-CHANNEL MOSFET SOR 223 package? 31-99V???
********************************************************************************
*ZETEX ZVN4310GTA Spice Model v1.1 Last Revised 8/2/02
.SUBCKT ZVN4310 3 4 5
*NODES: DRAIN GATE SOURCE
M1 3 2 5 5 MOD1
RG 4 2 82
RL 3 5 1E7
C1 2 5 200E-12
C2 3 2 13.5E-12 
D1 5 3 DIODE1
*
.MODEL MOD1 NMOS VTO=2.555 RS=0.2068 RD=0.1665 IS=1E-15 KP=5.02
+CBD=288E-12 PB=1 LAMBDA=0
.MODEL DIODE1 D IS=9.759E-13 N=1.02476 RS=0.0844
.ENDS ZVN4310A

* R6535KNZ NMOSFET model
* Model Generated by ROHM
* All Rights Reserved
* Commercial Use or
* Resale Restricted
* Date: 2019/9/11
*****************D G S
.SUBCKT R6535KNZ 1 2 3
M1 11 22 3 3 MOS_N
D1 3 1 DDS1
R1 1 11 RTH 93.237E-3
RG 2 22 5.7246
D14 22 11 DGD1
.MODEL MOS_N NMOS TNOM = 25
+ LEVEL = 3
+ L = 2.0000E-6
+ W = 3
+ KP = 23.046E-6
+ RS = 0
+ RD = 0
+ VTO = 4.5846
+ TOX = 2.0000E-6
+ CGSO = 864.20E-12
+ CGDO = 4.8643E-12
+ CBD = 1.0000E-15
+ GAMMA = 2.905
+ ETA = 0
+ KAPPA = 1
+ NFS = 27.158E+9
+ XJ = 1.0000E-6
+ UO = 117.64
+ VMAX = 50.000E+3
.MODEL DDS1 D TNOM = 25
+ IS = 2.8763E-15
+ N = 0.7852
+ RS = 4.5054E-3
+ IKF = 0.1019
+ CJO = 17.880E-9
+ M = 1.2527
+ VJ = 5.3941
+ XTI = 3.1883
+ BV = 650
+ TT = 1.0128E-6
.MODEL RTH RES TNOM = 25
+ TC1 = 7.8933E-3
+ TC2 = 22.403E-6
.MODEL DGD1 D TNOM = 25
+ IS = 1.0000E-30
+ N = 100
+ RS = 1.0000E-3
+ CJO = 1.5121E-9
+ M = 2.7922
+ VJ = 12.131
+ FC = 10.000E-3
+ T_ABS = 25
.ENDS

.end
