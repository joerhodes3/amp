* ===========================
* 12AX7 SPICE Model by Norman Koren
* ===========================
.SUBCKT 12AX7_single 1 2 3  ; P G C;  NEW MODEL
+ PARAMS: MU=100 EX=1.4 KG1=1060 KP=600 KVB=300 RGI=2000 
+ CCG=2.3P  CGP=2.4P CCP=.9P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE={V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID
C2 2 1 {CGP}  ; GRID=PLATE
C3 1 3 {CCP}  ; CATHODE-PLATE
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 12AX7_dual 1 2 3 4 5 6 7 8 9
* pin 4 id H1
* pin 5 is H2
* pin 9 is HGND
XUB 1 2 3 12AX7_single
XUA 6 7 8 12AX7_single
.ENDS 12AX7_dual

* ===========================
* KT88 SPICE Model
* ===========================
*.SUBCKT KT88 1 2 3 4 ;  P G1 C G2
* pin 1,6,8 unused
.SUBCKT KT88 nc 2 3 heater+ heater- nc 1 nc 4
+ PARAMS: MU=8.8 EX=1.35 KG1=730 KG2=4800 KP=32 KVB=16
+         CCG=14P CPG1=.85P CCP=12P RGI=1K 

RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE={V(4,3)/KP*LOG(1+EXP((1/MU+V(2,3)/V(4,3))*KP))}
G1   1 3  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,3)/KVB)}
* Safe, clamped version of G2 to avoid log(negative)
G2   4 3  VALUE={(EXP(EX*LOG(LIMIT((V(4,3)/MU)+V(2,3), 1e-6, 1e6))))/KG2}

RCP  1 3  1G      ; FOR CONVERGENCE
C1   2 3  {CCG}    ; CATHODE-GRID 1
C2   1 2  {CPG1}  ; GRID 1-PLATE
C3   1 3  {CCP}   ; CATHODE-PLATE
R1   2 5  {RGI}   ; FOR GRID CURRENT
D3   5 3  DX      ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS KT88

* ===========================
* CD4066 Analog Switch (1 Switch)
* ?other pins?
* ===========================
.SUBCKT CD4066_single A B CTRL
S1 A B CTRL CTRL SWMODEL
.MODEL SWMODEL SW(Ron=100 Roff=1Meg Vt=2.5 Vh=0.1)
.ENDS CD4066_single

.SUBCKT CD4066_quad 1 2 3 4 5 6 7 8 9 10 11 12 13 14
* pin 7 VSS / pin 14 VDD
XUA 1 2 13 CD4066_single
XUB 3 4 5 CD4066_single
XUC 8 9 6 CD4066_single
XUD 10 11 12 CD4066_single
.ENDS CD4066_quad

* ===========================
* isolation transformer
* ===========================
.SUBCKT XFMR 1 2 3 4
L1 1 2 1000
L2 3 4 1000
K1 L1 L2 .9999999
.ENDS

* ===========================
* output transformer
* ===========================
.SUBCKT Ham1650P P1 Sg1 B Sg2 P2 O16 O8 O4 Com				
* Push Pull transformer, with Ultralinear taps at 40%				
* 6600 to 16 ohms, 3db 15 to 60000 hz				
* speaker taps at 8 and 4 ohms				
*  Hammond 1650P				
*   				
*  model generated by TransformerModels.xls 2025 May 11				
*  				
LP1 1 Sg1 3.19344114792738				
LS1 2 B 1.41930717685661				
LS2 3 Sg2 1.41930717685661				
LP2 4 P2 3.19344114792738				
LA1 5 O8 0.00741645776380232				
LA2 6 O4 0.00370822888190116				
LA3 7 Com 0.02161314180004				
KALL LP1 LS1 LS2 LP2 LA1 LA2 LA3 0.998434750532355				
RP1 P1 1 30				
RP2 Sg1 2 15				
RP3 B 3 15				
RP4 Sg2 4 30				
RS1 O16 5 0.1				
RS2 O8 6 0.1				
RS3 O4 7 0.1				
.ENDS Ham1650P				
* Acro Elco HK Peerless Dynaco Leak Gen Edcor

* ===========================
* switch open (RCA)
* ===========================
.SUBCKT switch_open 1 2
R1 1 2 10e10
.ENDS switch_open

* ===========================
* switch closed (XLR)
* ===========================
.SUBCKT switch_closed 1 2
R1 1 2 1
.ENDS switch_closed

* ===========================
* button
* ===========================
.SUBCKT button 1 2
R1 1 2 10e10
.ENDS button

* ===========================
* 74HC14 - simplified from TI model, but don't care
* ===========================
.subckt sn74hc14 1 2 3 4 5 6 7 8 9 10 11 12 13 14

* SOT14
* pin 7 VSS, pin 14 VCC
X1 1 2 74HC14
X2 3 4 74HC14
X3 5 6 74HC14
X4 9 8 74HC14
X5 11 10 74HC14
X6 13 12 74HC14

.ends
**********************
*SRC=74HC14;74HC14;CMOS;74HCxx;inverter Schmitt
*SYM=INV
*74HC14 HEX SCHMITT-TRIGGER INVERTERS
*CAUTION: input is analog while output is digital
***
.subckt 74HC14 in out
ainv a out ls_inv
aadc  [in] [a] ls_adc

.model ls_inv d_inverter(rise_delay=19n fall_delay=19n)
.model ls_adc adc_bridge(in_low=2.35 in_high=1.67)
.ends

* ===========================
* LM317 -- BIAS (so all about the current extration -- 0 = (1 +R2/R1)*1.25
* ===========================
.SUBCKT LM317 1    2        3
*Connections  Adj. Output   Input
*LM317 voltage regulator - Texas Instruments
J1 1 3 4 JN
Q2 5 5 6 QPL .1
Q3 5 8 9 QNL  .2
Q4 8 5 7 QPL .1
Q5 81 8 3 QNL .2
Q6 3 81 10 QPL .2
Q7 12 81 13 QNL  .2
Q8 10 5 11 QPL  .2 
Q9 14 12 10 QPL .2
Q10 16 5 17 QPL  .2
Q11 16 14 15 QNL .2
Q12 3 20 16 QPL .2
Q13 1 19 20 QNL .2
Q14 19 5 18 QPL .2
Q15 3 21 19 QPL .2
Q16 21 22 16 QPL .2
Q17 21 3 24 QNL   .2
Q18 22 22 16 QPL .2
Q19 22 3 241 QNL 2
Q20 3 25 16 QPL .2
Q21 25 26 3 QNL .2
Q22A 35 35 1 QPL 2
Q22B 16 35 1 QPL 2
Q23 35 16 30 QNL  2
Q24A 27 40 29 QNL .2
Q24B 27 40 28 QNL .2
Q25 1 31 41 QNL 5
Q26 1 41 32 QNL 50
D1 3 4 DZ
D2 33 1 DZ
D3 29 34 DZ
R1 1 6 310
R2 1 7 310
R3 1 11 190
R4 1 17 82
R5 1 18 5.6K
R6 4 8 100K
R7 8 81 130
R8 10 12 12.4K
R9 9 3 180
R10 13 3 4.1K
R11 14 3 5.8K
R12 15 3 72
R13 20 3 5.1K
R14 2 24 12K
R15 24 241 2.4K
R16 16 25 6.7K
R17 16 40 12K
R18 30 41 130
R19 16 31 370
R20 26 27 13K
R21 27 40 400
R22 3 41 160
R23 33 34 18K
R24 28 29 160
R25 28 32 3
R26 32 3 .1
C1 21 3 30PF
C2 21 2 30PF
C3 25 26 5PF
CBS1 5 3 2PF
CBS2 35 3 1PF
CBS3 22 3 1PF
.MODEL JN NJF(BETA=1E-4 VTO=-7)
.MODEL DZ D(BV=6.3)
.MODEL QNL NPN(EG=1.22 BF=80 RB=100 CCS=1.5PF TF=.3NS TR=6NS CJE=2PF
+ CJC=1PF VAF=100)
.MODEL QPL PNP(BF=40 RB=20 TF=.6NS TR=10NS CJE=1.5PF CJC=1PF VAF=50)
.ENDS LM317

********************************************************************************
* OPA2156
* Is a DIP8 Package
********************************************************************************
.SUBCKT OPAMP_DIP8 1 2 3 4 5 6 7 8
* TI model has issues with ESD clamps due to switch
X_opamp_A 3 2 4 8 1 OPAMP2
X_opamp_B 5 6 4 8 7 OPAMP2
.ENDS
